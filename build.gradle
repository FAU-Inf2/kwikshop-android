// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'org.ajoberstar:gradle-git:1.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

import org.ajoberstar.grgit.*


def getGitRepository() {
    def repo = Grgit.open(dir: project.getRootProject().file('.'))
    return repo
}

def getGitCommitId() {
    def repo = getGitRepository()
    def commitId = repo.head().getAbbreviatedId(10)
    if(!repo.status().isClean()) {
        commitId += "+"
    }
    return commitId
}


def getGitBranchName() {
    def repo = getGitRepository()
    return repo.branch.getCurrent().trackingBranch.getName()
}

allprojects {
    repositories {
        jcenter()
    }

    // writes info from git to build/info.txt for each project
    task writeBuildInfoFile() << {

        def buildInfoFile = project.file("build/info.txt")

        def buildInfo = "Commit: "
        buildInfo <<= getGitCommitId()
        buildInfo <<= "\n"

        buildInfo <<= "Branch: "
        buildInfo <<= getGitBranchName()
        buildInfo <<= "\n"

        buildInfoFile.text = buildInfo
    }

}




